RES=""
# ------
L=「lexer.rs」を開く。
Lから「<CHAR_COMMANDS>」を切り取る。
CMD_LIST=対象から「</CHAR_COMMANDS>」を切り取る。
CMD_LIST=CMD_LIST&改行。
Lから「<UPPER_COMMANDS>」を切り取る。
CMD_LIST_U=対象から「</UPPER_COMMANDS>」を切り取る。

L=「sutoton.rs」を開く。
Lから「<SUTOTON>」を切り取る。
S_LIST=対象から「</SUTOTON>」を切り取る。
S_LISTを改行で区切って反復
　　S=対象。
　　Ｓで「SutotonItem::from」が何文字目。
　　もし、それ＝0ならば、続ける。
　　それはＳ。
　　「items.push(SutotonItem::from(」を「」に置換
　　「));」を「」に置換。
　　トリム
　　Ｓ＝それ
　　SS=Ｓを「,」で区切る。
　　Ｓ名前＝トリム（SS[0]）
　　Ｓ定義＝トリム(ＳＳ[1])
　　Ｓ＝S名前＆「=> // @ 定義: {S定義}」
　　Ｓを表示。
　　CMD_LIST=CMD_LIST&S&改行。
ここまで。

# ------
# UPPER COMMANDS
CMD_LIST_Uを改行で区切って反復
　　対象で「// @」が何文字目
　　もし、それ=0ならば続ける。
　　それは対象
　　「cmd == 」を「」に置換。
　　「if 」を「」に置換。
　　『{』を「=>」に単置換。
　　トリム
　　LINE=それ
　　CMD_LIST=CMD_LIST&LINE&改行。
　　LINEを表示。
ここまで。

# --------
# LOWER COMMANDS
CMD_LISTを改行で区切って反復
　　トリム。LINE=それ
　　LINEで「// @」が何文字目
　　もし、それ＝0ならば、続ける。
　　LINEから「=>」を切り取って、CMDに代入。
　　対象から「// @」を切り取る。
　　説明＝対象。
　　CMD=CMDの「|」を「 」に置換。
　　CMD=CMDの『\s{2,}』を「 」に正規表現置換。
　　CMD=トリム(CMD)
　　RES=RES&「|{CMD}|{説明}|」&改行
ここまで。

MD=「
# テキスト音楽 サクラ(Rust版) コマンド一覧

| コマンド | 説明    |
|---------|--------|
{RES}
」
MD=トリム(MD)
MDを表示。
MDを「command.md」に保存。
「ok」と表示。
